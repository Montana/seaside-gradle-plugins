def projects = [
      'bonjourlemonde',
      'helloworld',
      'holamundo'
]

gradle.startParameter.taskNames.each { taskName ->
    task(taskName)
}

def previous = null
projects.each { projectName ->
    def subtaskStartParameter = gradle.startParameter.newInstance()
    subtaskStartParameter.buildFile = null
    subtaskStartParameter.settingsFile = null
    subtaskStartParameter.projectCacheDir = null
    subtaskStartParameter.projectDir = file(projectName)
    subtaskStartParameter.currentDir = subtaskStartParameter.projectDir

    def subtask = task(projectName, type: GradleBuild) {
        startParameter = subtaskStartParameter
    }

    gradle.startParameter.taskNames.each { taskName ->
        tasks.getByName(taskName).dependsOn(subtask)
    }

    if (previous != null) {
        subtask.mustRunAfter(previous)
    }

    previous = subtask
}
