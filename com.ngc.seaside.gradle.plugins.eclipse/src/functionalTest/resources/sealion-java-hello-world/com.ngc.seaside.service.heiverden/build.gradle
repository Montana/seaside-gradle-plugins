plugins { 
   id 'com.ngc.seaside.repository'
   id 'com.ngc.seaside.eclipse.updatesite'
   id 'com.ngc.seaside.eclipse.p2'
}

eclipseDistribution {
   linuxVersion = "eclipse-dsl-oxygen-2-linux-gtk-x86_64"
   windowsVersion = "eclipse-dsl-oxygen-2-win32-x86_64"
   linuxDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${linuxVersion}.zip"
   windowsDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${windowsVersion}.zip"
   enablePluginsRepository()

   // Exclude transitive plugin dependencies that are already bundled with eclipse
   unzipEclipse.doLast {
      configurations {
         plugin {
            pluginsDirectory.eachFileMatch({ it.endsWith '.jar' }) {
               exclude module: it.name.substring(0, it.name.indexOf('_'))
            }
         }
      }
   }
}

eclipseUpdateSite {

   def sdFeature = feature {
      id = 'com.ngc.seaside.systemdescriptor.feature'
      label = 'JellyFish SystemDescriptor DSL'
      version = project.version
      providerName = 'Northrop Grumman Corporation'
      description {
         url = 'http://www.systemdescriptor.seaside.ngc.com/description'
         text = 'This is the JellyFish System Descriptor Domain Specific Language Eclipse plugin.'
      }
      copyright {
         url = 'http://www.systemdescriptor.seaside.ngc.com/copyright'
         text = project.resources.text.fromFile(project.file('src/main/resources/license.txt')).asString()
      }
      license {
         url = 'http://www.systemdescriptor.seaside.ngc.com/license'
         text = copyright.text
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor'
         version = '0.0.0'
         unpack = false
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor.ui'
         version = '0.0.0'
         unpack = false
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor.ide'
         version = '0.0.0'
         unpack = false
      }
      plugin {
         id = 'org.glassfish.javax.json'
         version = '0.0.0'
         unpack = false
      }
      include {
         id = 'cucumber.eclipse.feature'
         name = 'Cucumber Eclipse Feature'
         required = true
      }
   }
   
   category {
      name = 'system_descriptor_category_id'
      label = 'System Descriptor Plugin'
      description = 'Eclipse Plugin for the Seaside System Descriptor'
      feature sdFeature
   }
}

p2.remoteRepository('https://cucumber.io/cucumber-eclipse/update-site') {
   plugins { p ->
      dependencies {
         plugin p.dependency
      }
   }
   features {
      eclipseUpdateSite.feature it
   }
}

dependencies {
   plugin project(':service.helloworld')
   plugin name: 'org.antlr.runtime_3.2.0.v201101311130'
   plugin 'org.glassfish:javax.json:1.1'
}

